prototype(Sitegeist.GoldenGate.Neos.PluginExample:Content.Product) < prototype(Neos.Neos:ContentComponent) {

    @context {
        shop = ${q(node).property('goldengate__shop')}
        product = Sitegeist.GoldenGate.Neos.Mixins:Shopware.Product {
            node = ${node}
        }
    }

    renderer = Neos.Fusion:Case {

        has {
            condition = ${product}
            renderer = Sitegeist.GoldenGate.Neos.PluginExample:Component.Teaser {
                title = ${product.name}
                description = ${String.stripTags(String.cropAtWord(product.description, 120, '...'))}
                imageUri = ${product.mainDetail.mainImage.imageSizes[2].url}
                priceValue = ${product.mainDetail.mainPrice.value}
                priceCurrency = ${product.mainDetail.mainPrice.currency}
                uri = ${product.mainDetail.uri}
                isLarge = true
            }
        }

        fallback {
            condition = true
            renderer = 'please select a product via the inspector'
        }
    }

    @cache {
        mode = 'cached'
        maximumLifetime = '86400'
        entryIdentifier {
            node = ${node}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Shopware.Caching.productTag(shop, product)}
        }
    }
}
